; /**
;  * Forces (Gravity and Fluid Resistence) with Vectors 
;  * by Daniel Shiffman.  
;  * 
;  * Demonstration of multiple force acting on bodies (Mover class)
;  * Bodies experience gravity continuously
;  * Bodies experience fluid resistance when in "water"
;  */
;  Ported to Extempore by Jason Levine
 
; // Liquid is a rectangle
; float x,y,w,h;
; // Coefficient of drag
; float c;
(bind-type Liquid <float,float,float,float,float>)

;; Is the Mover in the Liquid?
(bind-func contains
	(lambda (l:Liquid* m:Mover*)
		(let ((loc_x:float (getx (tref m 0)))
					(loc_y:float (gety (tref m 0)))
					(liq_x:float (tref l 0))
					(liq_y:float (tref l 1))
					(liq_w:float (tref l 2))
					(liq_h:float (tref l 3)))

			(and (> loc_x liq_x) 
				(< loc_x (+ liq_x liq_w)) 
				(> loc_y liq_y) 
				(< loc_y (+ liq_y liq_h))))))

;; Calculate drag force
(bind-func drag
	(lambda (l:Liquid* m:Mover*)
		(let ((velocity:vec2* (salloc)))
			(v2set velocity (tref m 1))

			;; Magnitude is coefficient * speed squared
			(let* ((coefficient:float (tref l 4))
						(speed:float (magnitude velocity))
						(drag_magnitude:float (* coefficient speed speed))
						(drag_force:vec2* (zalloc)))

				;; Direction is inverse of velocity
				(v2set drag_force velocity)
				(v2scale drag_force -1.)

				;; Scale according to magnitude
				(normalize drag_force)
				(v2scale drag_force drag_magnitude)
				drag_force))))


(bind-func display
	(lambda (l:Liquid*)
		(nostroke)
		(fill 50)
		(rectangle (tref l 0) (tref l 1) (tref l 2) (tref l 3))))