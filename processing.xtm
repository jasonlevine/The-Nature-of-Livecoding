;;window and renderer setup
(sys:load "libs/external/glfw3.xtm")

(bind-val window GLFWwindow*
  (begin
    (glfwSetErrorCallback (convert (get_native_fptr glfw_error_callback)))
    (glfw_init_and_create_interaction_window (convert width) (convert height))))

(sys:load "libs/external/nanovg.xtm")
(bind-val vg NVGcontext* (nvg_create_context))
(bind-val pixel_ratio float (glfw_get_pixel_ratio window))


;; wrappers for processing like functions and vars
;; background
(bind-func background
	(lambda (r:float g b)
		  (_nvgFillColor vg (NVGcolor r g b 1.0))
      (nvgBeginPath vg)
      (nvgRect vg 0. 0. width height)
      (nvgFill vg)))


;; fill
(bind-func fill
	(lambda (r:float g b a)
		(_nvgFillColor vg (NVGcolor r g b a))))     


;;stroke
(bind-func stroke
	(lambda (r:float g b a)
		(_nvgStrokeColor vg (NVGcolor r g b a))))   

(bind-func stroke_width
	(lambda (width:float)
		(nvgStrokeWidth vg width)))


;; circle
(bind-func circle
	(lambda (x:float y radius)
		(nvgBeginPath vg)
    (nvgCircle vg x y radius)
    (nvgFill vg)
    (nvgStroke vg)))

(bind-func rect
	(lambda (x:float y w h)
		(nvgBeginPath vg)
    (nvgRect vg x y w h)
    (nvgFill vg)
    (nvgStroke vg)))


;; -------VECTOR-------
;;  functions to work with 2d vectors 
(sys:load "libs/core/math.xtm")

(bind-alias vec float*)

(bind-func vec2_inc
  (lambda (v1:vec v2:vec)
    (vsumf v1 v2 2 v1)))

(bind-func vec2_get_x
  (lambda (v1:vec)
    (pref v1 0)))

(bind-func vec2_set_x
  (lambda (v1:vec val:float)
    (pset! v1 0 val)))

(bind-func vec2_get_y
  (lambda (v1:vec)
    (pref v1 1)))

(bind-func vec2_set_y
  (lambda (v1:vec val:float)
    (pset! v1 1 val)))

(bind-func vec2_set
  (lambda (v1:vec x:float y:float)
    (pset! v1 0 x)
    (pset! v1 1 y)))

;;  ------DRAW LOOP------
;; empty draw func to be reevaluated
(bind-func draw
    (lambda ()
    	(background 0.0 0.0 0.0)))

;; draw loop
(bind-func nvg_draw_loop
	(let ((frame 0))
  (lambda (time:i64 delta_t:double)
    ;(nvg_clear)
    (nvgBeginFrame vg (convert width) (convert height) pixel_ratio)
    (nvgResetTransform vg)
    (draw)
    (nvgEndFrame vg)
    (set! frame (+ frame 1)) 
    (glfwPollEvents)
    (glfwSwapBuffers window) 
    (let ((next_time (+ time (convert (* 44100. delta_t)))))
      (callback next_time nvg_draw_loop next_time delta_t)))))

;;start draw loop
(nvg_draw_loop (now) (/ 1. 60.))

;; to stop the draw loop, eval this version of nvg_draw_loop
;; (bind-func nvg_draw_loop
;;   (lambda (time:i64 delta_t:double)
;;     (println "nvg_draw_loop callback stopped")
;;     #f))

